plugins {
    id 'java'
    id 'org.springframework.boot' version '3.4.5'
    id 'io.spring.dependency-management' version '1.1.7'
    id 'jacoco'
}

group = 'cwchoiit'
version = '0.0.1'

bootJar.enabled = false
jar.enabled = true

allprojects {

    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'jacoco'

    repositories {
        mavenCentral()
    }

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.kafka:spring-kafka'

        testImplementation 'org.springframework.boot:spring-boot-starter-test'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

        implementation 'org.springframework.boot:spring-boot-starter'
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
    }

    tasks.named('test') {
        useJUnitPlatform()
        finalizedBy('jacocoTestReport')
    }

    jacocoTestReport {
        dependsOn('test')
        reports {
            xml.required = true
            html.required = true
        }
        finalizedBy('jacocoTestCoverageVerification')
    }

    check.dependsOn jacocoTestCoverageVerification

    jacocoTestCoverageVerification {
        violationRules {
            rule {
                element = 'BUNDLE'
                includes = ['cwchoiit.blackfriday.order.service.*']

                limit {
                    counter = 'LINE'
                    value = 'COVEREDRATIO'
                    minimum = 0.99
                }
            }
        }
    }
}

tasks.register("cleanSubModules") {
    dependsOn(
            ":service:catalog:clean",
            ":service:delivery:clean",
            ":service:member:clean",
            ":service:order:clean",
            ":service:payment:clean",
            ":service:search:clean"
    )
}

tasks.register("buildOnlySubModules") {
    dependsOn(
            ":service:catalog:build",
            ":service:delivery:build",
            ":service:member:build",
            ":service:order:build",
            ":service:payment:build",
            ":service:search:build"
    )
}